// For those who want the bleeding edge
buildscript {
    repositories {
        jcenter()
        maven {
            name = "forge"
            url = "http://files.minecraftforge.net/maven"
        }
    }
    dependencies {
        classpath 'net.minecraftforge.gradle:ForgeGradle:2.3-SNAPSHOT'
    }
}

apply plugin: 'net.minecraftforge.gradle.forge'

sourceCompatibility = JavaVersion.VERSION_1_8
targetCompatibility = JavaVersion.VERSION_1_8

ext.configFile = file('build.properties')
ext.config = parseConfig(configFile)

version = config.mc_version + "-" + config.mod_version

archivesBaseName = 'modular-routers'

if (System.getenv().BUILD_NUMBER != null) {
    version += "-${System.getenv().BUILD_NUMBER}"
} else {
    version += "-CUSTOM"
}

minecraft {
    version = config.mc_version + "-" + config.forge_version
    mappings = config.mcp_mappings
    runDir = "run"
    replace '@VERSION@', project.version
	replaceIn 'ModularRouters.java'
}

repositories {
    jcenter { }
    flatDir {
        dir 'libs/'
    }
    maven { // TOP
        name = "tterrag maven"
        url = "http://maven.tterrag.com/"
    }
//    maven {
//        name = "guideapi/hwyla"
//        url = 'http://tehnut.info/maven/'
//    }
    maven {
        name = "tesla"
        url = "http://maven.mcmoddev.com"
    }
    maven {
        name = "JEI"
        url = "http://dvs1.progwml6.com/files/maven"
    }
    maven {
        name = "CoFH Maven"
        url = "http://maven.covers1624.net"
    }
    maven {
        name = "K-4u Mod maven"
        url = "https://modmaven.k-4u.nl/"
    }
}

dependencies {
    deobfCompile "mcp.mobius.waila:Hwyla:${config.waila_version}:api"
    deobfCompile "mcjty.theoneprobe:TheOneProbe-1.12:${config.top_version}"
    deobfCompile "info.amerifrance.guideapi:Guide-API:1.12-${config.guideapi_version}"
    deobfCompile "mezz.jei:jei_1.12.2:${config.jei_version}"
    deobfCompile "cofh:RedstoneFlux:1.12-${config.redstone_flux_version}:universal"
    provided "com.google.errorprone:error_prone_annotations:2.1.0"
    compile "net.darkhax.tesla:Tesla-1.12:${config.tesla_version}"
    compile "team.chisel.ctm:CTM:MC1.12.2-0.3.2.18:api"
}

processResources {
    // this will ensure that this task is redone when the versions change.
    inputs.property "version", project.version
    inputs.property "mcversion", project.minecraft.version

    // replace stuff in mcmod.info, nothing else
    from(sourceSets.main.resources.srcDirs) {
        include 'mcmod.info'

        // replace version and mcversion
        expand 'version':project.version, 'mcversion':project.minecraft.version
    }

    // copy everything else, thats not the mcmod.info
    from(sourceSets.main.resources.srcDirs) {
        exclude 'mcmod.info'
    }
}

idea { module { inheritOutputDirs = true } }

def parseConfig(File config) {
    config.withReader {
        def prop = new Properties()
        prop.load(it)
        return (new ConfigSlurper().parse(prop))
    }
}
